package com.redhatkeynote.score

import function com.redhatkeynote.score.ScoreServer.logger
import function com.redhatkeynote.score.ScoreServer.server

rule "init game"
    when
    then
        logger().info("init game");
        server().init(kcontext);
end

rule "submit player score"
    when
        player : Player()
    then
        logger().info("submit player score");
        server().savePlayer(player);
        insertLogical(new Achiever(player.getUuid(), player.getScore()));
end

rule "100 points achievement"
    when
        achiever : Achiever(score > 99 && score < 500)
    then
        logger().info("100 points achievement");
        achiever.setAchievement("100_POINTS");
        server().reportAchievement(achiever);
end

rule "500 points achievement"
    when
        achiever : Achiever(score > 500)
    then
        logger().info("500 points achievement");
        achiever.setAchievement("500_POINTS");
        server().reportAchievement(achiever);
end

rule "broadcast scores"
    timer ( int: 2s 2s )
    when
    then
        logger().info("broadcast scores");
        server().broadcastScores(10);
end
