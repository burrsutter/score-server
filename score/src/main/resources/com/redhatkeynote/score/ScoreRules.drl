package com.redhatkeynote.score

import function com.redhatkeynote.score.ScoreServer.logger
import function com.redhatkeynote.score.ScoreServer.server
import java.util.List

rule "init game"
        salience 100
    when
    then
        logger().info("init game");
        server().init(kcontext);
end

rule "load and merge player"
        salience 80
    when
        player : Player(id == null)
    then
        Player p = server().loadPlayer(player);
        update(player__Handle__, p);
end

rule "Create achievement if it does not exist"
        salience -3
    when
        pach   : PlayerAchievement( )
        not( Achievement( description == pach.achievement ) from server().loadAchievements() )
    then
        server().createAchievement( pach.getAchievement() );
        update( pach );
end

rule "process achievement"
        salience -5
    when
        pach   : PlayerAchievement( )
        ach    : Achievement( description == pach.achievement ) from server().loadAchievements()
        player : Player( uuid == pach.uuid, !hasAchievement(ach) )
    then
        logger().info("Processing player achievement: "+player.getUuid()+" -> "+ach.getDescription() );
        Achievement na = ach.clone();
        na.setNewAchievement(true);
        player.addAchievement( na );
end

rule "submit player score"
        salience -10
    when
        player : Player()
    then
        logger().info("submit player score");
        server().savePlayer(player);
end


rule "delete players"
    when
        deletePlayers : DeletePlayers()
    then
        server().deletePlayers();
end
